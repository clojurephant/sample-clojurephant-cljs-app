plugins {
  id 'dev.clojurephant.clojurescript' version '0.8.0-beta.4'
  id 'distribution'
}

group = 'my.group'
version = '0.1.0-SNAPSHOT'

repositories {
  mavenCentral()
  maven {
    name = 'clojars'
    url = 'https://repo.clojars.org'
  }
}

dependencies {
  implementation 'org.clojure:clojure:1.11.1'
  implementation 'org.clojure:clojurescript:1.11.60'

  devImplementation('dev.clojurephant:clojurephant-tooling:0.1.0-rc.2') {
    capabilities {
      requireCapability 'dev.clojurephant:clojurephant-tooling-figwheel-main'
    }
  }
}

clojurescript {
  builds {
    all {
      compiler {
        outputTo = 'public/js/main.js'
        outputDir = 'public/js/out'
        main = 'my.group.myapp'
        assetPath = 'js/out'
      }
    }
    main {
      compiler {
        optimizations = 'advanced'
        sourceMap = 'public/js/main.js.map'
      }
    }
    dev {
      compiler {
        optimizations = 'none'
        sourceMap = true
        preloads = ['my.group.myapp.dev']
      }
      figwheel {
        openUrl = 'http://localhost:9500/index.html'
      }
    }
  }
}

distributions {
  main {
    contents.from sourceSets.main.output
  }
}
