plugins {
  id 'dev.clojurephant.clojurescript' version '0.7.0-alpha.3'
  id 'distribution'
}

group = 'my.group'
version = '0.1.0-SNAPSHOT'

repositories {
  mavenCentral()
  maven {
    name = 'clojars'
    url = 'https://repo.clojars.org'
  }
}

dependencies {
  implementation 'org.clojure:clojure:1.11.1'
  implementation 'org.clojure:clojurescript:1.11.60'
  devImplementation 'cider:piggieback:0.5.3'
  devImplementation 'com.bhauman:figwheel-repl:0.2.18'
  devImplementation 'org.eclipse.jetty.websocket:websocket-servlet:9.4.36.v20210114'
  devImplementation 'org.eclipse.jetty.websocket:websocket-server:9.4.36.v20210114'
  devImplementation('ring:ring-jetty-adapter:1.9.5') {
    because 'Needed by figwheel-repl'
  }
}

clojurescript {
  builds {
    all {
      compiler {
        outputTo = 'public/js/main.js'
        outputDir = 'public/js/out'
        main = 'my.group.myapp'
        assetPath = 'js/out'
      }
    }
    main {
      compiler {
        optimizations = 'advanced'
        sourceMap = 'public/js/main.js.map'
      }
    }
    dev {
      compiler {
        optimizations = 'none'
        sourceMap = true
        preloads = ['my.group.myapp.dev']
      }
    }
  }
}

tasks.named("clojureRepl") {
  middleware.add("cider.piggieback/wrap-cljs-repl")
}

distributions {
  main {
    contents.from sourceSets.main.output
  }
}
